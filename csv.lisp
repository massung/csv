;;;; CSV Parsing for Common Lisp
;;;;
;;;; Copyright (c) Jeffrey Massung
;;;;
;;;; This file is provided to you under the Apache License,
;;;; Version 2.0 (the "License"); you may not use this file
;;;; except in compliance with the License.  You may obtain
;;;; a copy of the License at
;;;;
;;;;    http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing,
;;;; software distributed under the License is distributed on an
;;;; "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
;;;; KIND, either express or implied.  See the License for the
;;;; specific language governing permissions and limitations
;;;; under the License.
;;;;

(defpackage :csv
  (:use :cl :lexer :parse)
  (:export
   #:csv-parse
   #:csv-format))

(in-package :csv)

;;; ----------------------------------------------------

(define-lexer csv-lexer (s)
  ("%n+"      (values :end))
  (","        (values :comma))

  ;; double quotes around a cell
  ("\""       (push-lexer s 'string-lexer :quote))

  ;; anything else is the cell
  (".[^%n,]*" (values :cell $$)))

;;; ----------------------------------------------------

(define-lexer string-lexer (s)
  ("\"\""     (values :chars "\""))

  ;; end of the string?
  ("\""       (pop-lexer s :quote))

  ;; anything else
  (".[^\"]*"  (values :chars $$)))

;;; ----------------------------------------------------

(define-parser csv-parser
  (.sep-by1 'csv-record (.is :end)))

;;; ----------------------------------------------------

(define-parser csv-record
  (.sep-by1 'csv-cell (.is :comma)))

;;; ----------------------------------------------------

(define-parser csv-cell
  (.one-of 'csv-string (.is :cell) (.ret "")))

;;; ----------------------------------------------------

(define-parser csv-string
  (.let (cs (>> (.is :quote) (.many-until (.is :chars) (.is :quote))))
    (.ret (format nil "狺泱┅┅换ㄤ彐躅泱霏疳蝮篝蜷铉镳糸镱犰箫躜沐⒚镱鲥螋糜篝蜷铉轭麸涕箴镡赍泗鏖翳戾弪戾弪с篥戾弪篝蜷铉后秕蜚箫躜沐鏖翳麸脲瞽蝈徜弪铄舡麸脲戾弪疳蝮с篥疳蝮弪铄舡麸脲瞟┅换ㄤ彐躅泱霏骘蝽狒蝈泔蜾镳糸镱犰篝蝈犴⒚镱鲥螋扉篝镦涕箴镡赍泗轭麸扉篝糜篝蜷铉ㄦ矧磲篝蝈犴泱龊烘矧磲舡沐祆蕃" record))

;;; ----------------------------------------------------

(defun format-cell (stream cell &optional colonp atp &rest args)
  "Format a CSV record cell to a stream."
  (declare (ignore colonp atp args))
  (let ((s (princ-to-string cell)))
    (if (find #\, s)
        (progn
          (write-char #\" stream)

          ;; output all the cell characters to the stream
          (loop
             for c across s

             ;; write this portion of the cell
             do (if (char= c #\")
                    (write-string "\"\"" stream)
                  (princ c stream))

             ;; close the string
             finally (write-char #\" stream)))
      (princ s stream))))
